📦 sockets 🏠
📦 files 🏠


💭💭💭💭💭💭 ValueType for HTTP request💭💭💭💭💭💭

🕊 🇰🇷 🍇

    💭 HTTP Method
    🖍🆕 방법 🔡

    💭 URI
    🖍🆕 우리 🔡

    💭 Host
    🖍🆕 주최자 🔡

    💭 httpVersion
    🖍🆕 버전 🔡

    💭 userAgent
    🖍🆕 에이전트 🔡

    💭 Constructor for this value type (struct)
    🆕 🍼 방법 🔡 🍼 우리 🔡 🍼 주최자 🔡 🍼 버전 🔡 🍼 에이전트 🔡 🍇🍉

    💭 Getter method for HTTP Method
    ❗️🧟‍♀️➡️ 🔡 🍇

        ↩️ 방법

    🍉

    💭 Getter method for URI
    ❗️🧟➡️ 🔡 🍇

        ↩️ 우리

    🍉

    💭 Getter method for Host
    ❗️👩‍🏭➡️ 🔡 🍇

        ↩️ 주최자

    🍉

    💭 Getter method for httpVersion
    ❗️👩‍🌾➡️ 🔡 🍇

        ↩️ 버전

    🍉

    💭 Getter method for userAgent
    ❗️🧞‍♀️➡️ 🔡 🍇

        ↩️ 에이전트

    🍉

    💭 Setter method for HTTP Method
    🖍❗️🧞 입력 🔡 🍇

        입력 ➡️🖍방법

    🍉

    💭 Setter method for URI
    🖍❗️🧞‍♂️ 입력 🔡 🍇

        입력 ➡️🖍우리

    🍉

    💭 Setter method for Host
    🖍❗️ 👨‍🍼 입력 🔡 🍇

        입력 ➡️🖍주최자

    🍉

    💭 Setter method for httpVersion
    🖍❗️🧜‍♀️ 입력 🔡 🍇

        입력 ➡️🖍버전

    🍉

    💭 Setter method for userAgent
    🖍❗️🧜 입력 🔡 🍇

        입력 ➡️🖍에이전트

    🍉

🍉


💭💭💭💭💭💭 Class for HTTP request check💭💭💭💭💭💭



🐇 🇰🇵 🍇

    💭Whole Request
    🖍🆕요청🔡

    💭 Request using ValueType
    🖍🆕대한민국🇰🇷

    💭 Default constructor
    🆕 🍼 요청 🔡 🍼 대한민국 🇰🇷 🍇 🍉

    💭 Constructor to extract required information
    🆕▶️🧜‍♂️ 🍼 요청 🔡 🍇

        💭 Check if request is NULL
        ↪️요청🙌🔤NULL🔤 🍇

            💭 Debugging prints
            💭😀🔤IN NULL REQUEST🔤❗️

            💭 Create a new valueType
            🆕🇰🇷 🔤NULL🔤 🔤NULL🔤 🔤NULL🔤 🔤NULL🔤 🔤NULL🔤❗️ ➡️ 테스트이

            💭 Put the newly create valueType into the class variable
            테스트이 ➡️ 🖍대한민국

        🍉

        🙅‍ 🍇
            💭 Debugging prints
            💭😀🔤Request start🔤❗️
            💭😀요청❗️
            💭😀🔤Request end🔤❗️

            💭 Split the request
            🔫 요청 🔤❌r❌n🔤❗️ ➡️ 머리글

            💭 Split the request header (GET / HTTP/1.1)
            🔫 🐽머리글 0❗️ 🔤 🔤❗️ ➡️ 의뢰

            💭 Creating a new dictionary to store the headers
            🆕🍯🐚🔡🍆❗️ ➡️ 🖍🆕헤더사전

            💭 For loop the splitted request and split the header name and value
            🔂 머리 머리글 🍇

                💭 Search for the first ":" position
                🔍 머리 🔤:🔤❗️ ➡️ 인덱스

                💭 If dont have continue...
                ↪️인덱스🙌🤷 🍇

                    💭Bullshit dont even have continue statement ...

                🍉

                💭 If have break it down into header name and it's value
                🙅‍ 🍇

                    💭 🔡🍺 인덱스❗️➡️ test3
                    🔧🔪 머리 🍺인덱스 ➕ 1 📏🎶머리❗️❓❗️❗️ ➡️ 값
                    📪🔪 머리 0 🍺인덱스❗️❗️ ➡️ 이름
                    값 ➡️🐽헤더사전 이름❗️

                🍉

            🍉

            💭 Create a new dictionary to store the required headers
            🆕🍯🐚🔡🍆❗️ ➡️ 🖍🆕 ㄹ머리글

            💭 Check if host is included within the request
            ↪️ 🐽헤더사전  🔤host🔤❗️ ➡️ a 🍇

                a ➡️🐽ㄹ머리글 🔤host🔤❗️

            🍉

            💭 Check if dont have put NULL
            🙅‍ 🍇

                🔤NULL🔤 ➡️🐽ㄹ머리글 🔤host🔤❗️

            🍉

            💭 Check if request list got three
            ↪️ 📏의뢰❓🙌3 🍇

                🐽의뢰 0❗️ ➡️🐽ㄹ머리글 🔤httpMethod🔤❗️

                🐽의뢰 1❗️ ➡️🐽ㄹ머리글 🔤uri🔤❗️

                🐽의뢰 2❗️ ➡️🐽ㄹ머리글 🔤httpver🔤❗️

            🍉

            💭 Check if dont have put NULL
            🙅‍ 🍇

                🔤NULL🔤 ➡️🐽ㄹ머리글 🔤httpMethod🔤❗️

                🔤NULL🔤 ➡️🐽ㄹ머리글 🔤uri🔤❗️

                🔤NULL🔤 ➡️🐽ㄹ머리글 🔤httpver🔤❗️

            🍉

            💭 Check if userAgent is included within the request
            ↪️ 🐽헤더사전  🔤user-agent🔤❗️ ➡️ b 🍇
                b ➡️🐽ㄹ머리글 🔤user-agent🔤❗️
            🍉

            💭 Check if dont have put NULL
            🙅‍ 🍇

                🔤NULL🔤 ➡️🐽ㄹ머리글 🔤user-agent🔤❗️

            🍉

            💭 Create a new valueType
            🆕🇰🇷 🔡🍺🐽ㄹ머리글 🔤httpMethod🔤❗️❗️ 🔡🍺🐽ㄹ머리글 🔤uri🔤❗️❗️ 🔡🍺🐽ㄹ머리글 🔤host🔤❗️❗️ 🔡🍺🐽ㄹ머리글 🔤httpver🔤❗️❗️ 🔡🍺🐽ㄹ머리글 🔤user-agent🔤❗️❗️❗️ ➡️ 테스트이

            💭 Put the newly create valueType into the class variable
            테스트이 ➡️ 🖍대한민국

            💭 Debugging print to test getter of httpMethods
            💭🧟‍♀️테스트이❗️ ➡️ 테스트이디버그일
            💭😀테스트이디버그일❗️
            💭🧟테스트이❗️ ➡️ 테스트이디버그이
            💭😀테스트이디버그이❗️
            💭👩‍🏭테스트이❗️ ➡️ 테스트이디버그삼
            💭😀테스트이디버그삼❗️
            💭👩‍🌾테스트이❗️ ➡️ 테스트이디버그사
            💭😀테스트이디버그사❗️
            💭🧞‍♀️테스트이❗️ ➡️ 테스트이디버그오
            💭😀테스트이디버그오❗️

        🍉

    🍉

    💭METHOD TO GET FILE PATH
    ❗️ 👯‍♀️ 파일경로🔡 ➡️ 🔡 🍇

        💭/ Request
        ↪️파일경로🙌🔤/🔤🍇

            ↩️🔤/var/www/html/index.html🔤

        🍉

        💭Create the path
        ↩️🔤/var/www/html🧲파일경로🧲🔤

    🍉

    💭METHOD TO TRY OPENING A FILE
    ❗️ 🦹 열다🔡 ➡️ 👌 🍇

        💭Create the path
        👯‍♀️👇 열다❗ ➡️ 파일디렉토리

        💭Try opening the file (If can open return true)
        🆗 🆕 📄 ▶️ 📜 파일디렉토리❗️ 🍇

            ↩️👍

        🍉

        💭Except
        🙅‍♂️ error 🍇

            ↩️👎

        🍉

    🍉

    💭METHOD TO CHECK IF FILE EXIST
    ❗️🦄➡️🔡🍇

        🧟대한민국❗️ ➡️ 파일이름경로

        💭 Getter is NULL, return false
        ↪️ 파일이름경로🙌🔤NULL🔤🍇

            ↩️🔤거짓을찾을수없습니다🔤

        🍉

        💭 Getter is /
        🙅↪️ 파일이름경로🙌🔤/🔤 🍇

            ↪️🤜🦹👇 🔤/index.html🔤❗️🤛🙌👍 🍇

              ↩️🔤사실은파일입니다🔤

            🍉

            🙅‍ 🍇

              ↩️🔤거짓을찾을수없습니다🔤

            🍉

        🍉

        🙅‍ 🍇

            ↪️🤜🦹👇 파일이름경로❗️🤛🙌👍 🍇

              💭 Check if directory
              🔫 파일이름경로 🔤.🔤❗️➡️ 디렉토리확인

              ↪️ 📏디렉토리확인❓🙌1 🍇

                ↩️🔤거짓파일이아님🔤

              🍉

              🙅‍ 🍇

                ↩️ 🔤사실은파일입니다🔤

              🍉

            🍉

            🙅‍ 🍇

              ↩️🔤거짓을찾을수없습니다🔤

            🍉

        🍉

    🍉

    💭Deinitialise
    ♻️ 🍇

        💭😀 🔤Ended🔤❗️

    🍉

    💭METHOD ONE
    💭CHECK for GET else return false
    ❗ 🖕🏿➡️👌🍇

        🧟‍♀️ 대한민국❗️➡️ 가져오기

        ↪️ 가져오기 🙌 🔤GET🔤🍇

            ↩️👍

        🍉

        🙅🍇

            ↩️👎

        🍉

    🍉

    💭METHOD TWO
    💭CHECK for HTTP/1.1 else return false

    ❗ 🖕🏽➡️👌🍇

        👩‍🌾 대한민국❗️➡️ 편물

        ↪️ 편물 🙌 🔤HTTP/1.1🔤🍇

            ↩️👍

        🍉

        🙅🍇

            ↩️👎

        🍉

    🍉

    💭METHOD THREE
    💭CHECK for valid userAgent else return false
    ❗🖕🏾➡️👌🍇

        🧞‍♀️ 대한민국❗️➡️ 곱슬곱슬하다
        ↪️ 곱슬곱슬하다🙌🔤NULL🔤🍇

            ↩️👎

        🍉

        🙅🍇

            💭 Search the list for a match in the list
            🍿 🔤Mozilla🔤 🔤curl🔤 🔤Gecko🔤 🔤Firefox🔤 🔤MSIE🔤 🔤Safari🔤 🔤AppleWebKit🔤 🔤Macintosh🔤 🔤Linux x86_64🔤 🔤Linux i686🔤 🔤Android🔤🍆 ➡️ 사용자에

            💭 Create a variable to loop through the list above
            🔂 나는 사용자에 🍇

                💭 Search main method list with variable
                🔍 곱슬곱슬하다 나는❗️ ➡️ 사에아
                ↪️사에아🙌🤷 🍇

                  💭 Do nothing

                🍉

                🙅🍇

                    ↩️👍

                🍉

            💭 If not part of the list
            🍉
            ↩️👎

        🍉

    🍉

    💭METHOD FOUR
    💭RETURN HTTP RESPOND CODE OR JUST RETURN THE HTTP RESPOND HERE IN STRING
    ❗ 🖕🏻➡️📇🍇

        💭 Dictionary of Error Responses
        🆕🍯🐚📇🍆❗️ ➡️ 🖍🆕 사전오류응답
        📇🔤HTTP/1.1 400 Bad Request❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 96❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>400 잘못된 요청</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤사백🔤❗️
        📇🔤HTTP/1.1 403 Forbidden❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 86❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>403 금지</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤사백삼🔤❗️
        📇🔤HTTP/1.1 404 Not found❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 97❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>404 찾을 수 없음</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤사백사🔤❗️
        📇🔤HTTP/1.1 405 Method Not Allowed❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 101❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>405 비 허가 된 방식</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤사백오🔤❗
        📇🔤HTTP/1.1 500 Internal Server Error❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 103❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>500 인터넷 서버 오류</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤오백🔤❗
        📇🔤HTTP/1.1 505 HTTP Version Not Supported❌r❌nContent-Type: text/html; charset=UTF-8❌r❌nContent-Length: 114❌r❌nConnection: close❌r❌n❌r❌n<! DOCTYPE html><html lang=ko><p>505 HTTP 버전이 지원되지 않음</p><img src="/images/error.jpeg"></html>❌r❌n🔤❗️ ➡️🐽사전오류응답 🔤오백오🔤❗

        💭 Boolean: GET Request
        🖕🏿❗➡️ 방법1검사기

        💭 Boolean: HTTP Version
        🖕🏽❗➡️ 방법2검사기

        💭 Boolean: User Agent
        🖕🏾❗➡️ 방법3검사기

        💭 Text: checkfileExist
        🦄❗➡️ 파일존재확인


        💭 Main Code: Response
        ↪️ 파일존재확인🙌🔤사실은파일입니다🔤🍇

            💭 Get File
            🧟대한민국❗️ ➡️ 방법사파일
            👯‍♀️👇방법사파일❗➡️ 방법사파일예배규칙서
            
            💭 Initialise Variable
            🔤NULL🔤 ➡️ 🖍🆕내파일확장자
            🔤NULL🔤 ➡️ 🖍🆕내파일최소유형

            💭 Get File Extension
            🔫 방법사파일예배규칙서 🔤.🔤❗️ ➡️ 방법사파일내선
            ↪️ 📏방법사파일내선❓🙌2 🍇

                🐽방법사파일내선 1❗️ ➡️ 🖍내파일확장자

            🍉

            💭 Get Content Type
            ↪️ 내파일확장자 🙌 🔤jpeg🔤🍇

                🔤image/jpeg🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤jpg🔤🍇

                🔤image/jpeg🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤png🔤🍇

                🔤image/png🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤js🔤🍇

                🔤text/javascript🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤css🔤🍇

                🔤text/css🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤ico🔤🍇

                🔤image/vnd.microsoft.icon🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤xml🔤🍇

                🔤text/xml🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤webmanifest🔤🍇

                🔤application/manifest+json🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅↪️내파일확장자 🙌 🔤html🔤🍇

                🔤text/html🔤 ➡️ 🖍내파일최소유형

            🍉

            🙅🍇

                🔤NULL🔤 ➡️ 🖍내파일최소유형

            🍉

            💭 Binary File Data
            🍺📇🐇📄 방법사파일예배규칙서 ❗ ➡ 내바이너리파일

            💭 GET CONTENT LENGTH
            🔡📏내바이너리파일❓❗️➡ 내파일길이
            ↪️ 방법1검사기🙌👎 🍇

                ↩️🍺🐽사전오류응답 🔤사백오🔤❗️

            🍉

            🙅↪️ 방법2검사기🙌👎 🍇

                ↩️🍺🐽사전오류응답 🔤오백오🔤❗️

            🍉

            🙅↪️ 내파일확장자 🙌 🔤NULL🔤🍇

                ↩️🍺🐽사전오류응답 🔤오백🔤❗️

            🍉

            🙅↪️ 내파일최소유형 🙌 🔤NULL🔤🍇

                ↩️🍺🐽사전오류응답 🔤오백🔤❗️

            🍉

            🙅↪️ 🤜방법1검사기🙌👍🤛 🙌 🤜방법2검사기🙌👍🤛 🙌 🤜방법3검사기🙌👍🤛 🍇

                💭 Success Response
                🔤HTTP/1.1 200 OK❌r❌nContent-Type: 🧲내파일최소유형🧲❌r❌nContent-Length: 🧲내파일길이🧲❌r❌nConnection: close❌r❌n❌r❌n🔤 ➡️ 괜찮은응답버퍼
                📇괜찮은응답버퍼❗➕ 내바이너리파일 ➡️ 괜찮은반응바이너리

                ↩️괜찮은반응바이너리

            🍉

            🙅 🍇

                ↩️🍺🐽사전오류응답 🔤사백🔤❗️

            🍉

        🍉

        🙅↪️ 파일존재확인🙌🔤거짓을찾을수없습니다🔤 🍇

            ↩️🍺🐽사전오류응답 🔤사백사🔤❗️

        🍉

        🙅↪️ 파일존재확인🙌🔤거짓파일이아님🔤 🍇

            ↩️🍺🐽사전오류응답 🔤사백삼🔤❗️

        🍉

        🙅 🍇

            ↩️🍺🐽사전오류응답 🔤사백사🔤❗️

          🍉

    🍉

🍉


💭💭💭💭💭💭 Socket 💭💭💭💭💭💭
🏁 🍇

    🍺🆕🏄 8888❗️ ➡️ 우리서버

💭    🔁 👍 🍇
💭        🍺 🙋 우리서버❗️ ➡️ 클라이언트소켓

        🔁 👍 🍇

            🍺 🙋 우리서버❗️ ➡️ 클라이언트소켓

            🆗 고객데이터 👂 클라이언트소켓 10000❗️ 🍇

                💭😀🔡📏고객데이터❓❗️❗️

                ↪️📏고객데이터❓▶️0🍇

                    🆕🇰🇵▶️🧜‍♂️ 🍺🔡고객데이터❗️❗️ ➡️ 우리의클라이언트버퍼
                    🍺💬 클라이언트소켓 🖕🏻우리의클라이언트버퍼❗️❗️

                🍉

            🍉

            🙅‍♀️ error 🍇 🍉

            🚪 클라이언트소켓 ❗️

        🍉

💭    🍉

🍉